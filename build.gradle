plugins {
    id 'groovy'
    id 'java'
}

group 'dev.stasys'
version '1.0-SNAPSHOT'

repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

jar {
    manifest {
        attributes "Main-Class": "dev.stasys.shipmentdiscount.App"
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
sourceSets {
    acceptance {
        java.srcDir file('src/acceptance/groovy')
        resources.srcDir file('src/acceptance/resources')
    }
}

task acceptance(type: Test, dependsOn: jar) {
    group 'Verification'
    description 'Acceptance tests'
    testClassesDirs = sourceSets.acceptance.output.classesDirs
    classpath = sourceSets.acceptance.runtimeClasspath
    systemProperties['jar.path'] = jar.getArchivePath()
}

dependencies {
    testCompile 'org.codehaus.groovy:groovy-all:2.5.11'
    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
    acceptanceCompile configurations.testCompile
    acceptanceCompile sourceSets.main.output
}
